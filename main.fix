// Provides a simple interface for running commands.
//
// Example usage: TBA but see [fixlang-docpage-generator](https://github.com/tttmmmyyyy/fixlang-docpage-generator/blob/main/main.fix).
module EZS;

import Std::{
    Array, Bool, IO, String, U8, Array::{to_iter, @}, I64::to_CInt, 
    IO::{eprintln, exit, exit_with_msg, IOFail::try}, Monad::{pure, when}, 
    String::{borrow_c_str_io, get_size, join}, ToString::to_string
};
import Subprocess::{run_string, ExitStatus};

// Context and configuration for EZS.
type Context = struct {
    // Print (to stderr) the command to be executed.
    // 
    // Similar to `set -x` in bash.
    execution_trace : Bool,
    // Print (to stderr) the stderr output of the command.
    // 
    // If this is not set, the stderr output will be discarded.
    eprint_stderr : Bool,
};

namespace Context {
    // Default configuration for EZS.
    // 
    // `execution_trace` is false, `eprint_stderr` is true.
    default : Context = Context {
        execution_trace : false,
        eprint_stderr : true
    };
}

// Run a command and return its stdout ("o"), stderr ("e") and exit code ("c").
//
// # Parameters
// - `commands`: The command to run, as an array of strings, e.g., `["ls", "-l"]`.
// - `ctx`: The context for EZS.
run_oec : Array String -> Context -> IO (String, String, U8);
run_oec = |commands, config| run_wi_oec(commands, "", config);

// Run a command and return its stdout ("o") and stderr ("e").
// If the command fails, the program will exit with code 1.
//
// # Parameters
// - `commands`: The command to run, as an array of strings, e.g., `["ls", "-l"]`.
// - `ctx`: The context for EZS.
run_oe : Array String -> Context -> IO (String, String);
run_oe = |commands, ctx| run_wi_oe(commands, "", ctx);

// Run a command and return its stdout ("o").
// 
// If the command fails, the program will exit with code 1.
//
// # Parameters
// - `commands`: The command to run, as an array of strings, e.g., `["ls", "-l"]`.
// - `ctx`: The context for EZS.
run_o : Array String -> Context -> IO String;
run_o = |commands, ctx| run_wi_o(commands, "", ctx);

// Run a command with input ("wi") and return its stdout ("o"), stderr ("e") and exit code ("c").
//
// # Parameters
// - `commands`: The command to run, as an array of strings, e.g., `["grep", "ERROR"]`.
// - `input`: The input to provide to stdin of the command.
// - `ctx`: The context for EZS.
run_wi_oec : Array String -> String -> Context -> IO (String, String, U8);
run_wi_oec = |commands, input, ctx| (
    let commands_str = commands.to_iter.join(" ");
    when(ctx.@execution_trace, 
        eprintln(commands_str)
    );;
    let ((out, err), status) = *Subprocess::run_string(commands.@(0), commands, input).try(exit_with_msg(1));
    when(ctx.@eprint_stderr && err.get_size > 0, eprintln(err));;
    let code = *_check_exit_status(status, commands_str);
    pure $ (out, err, code)
);

// Run a command with input ("wi") and return its stdout ("o") and stderr ("e").
//
// # Parameters
// - `commands`: The command to run, as an array of strings, e.g., `["grep", "ERROR"]`.
// - `input`: The input to provide to stdin of the command.
// - `ctx`: The context for EZS.
run_wi_oe : Array String -> String -> Context -> IO (String, String);
run_wi_oe = |commands, input, ctx| (
    let (out, err, code) = *run_wi_oec(commands, input, ctx);
    _check_exit_code(code, commands.to_iter.join(" "));;
    pure $ (out, err)
);

// Run a command with input ("wi") and return its stdout ("o").
//
// If the command fails, the program will exit with code 1.
//
// # Parameters
// - `commands`: The command to run, as an array of strings, e.g., `["grep", "ERROR"]`.
// - `input`: The input to provide to stdin of the command.
// - `ctx`: The context for EZS.
run_wi_o : Array String -> String -> Context -> IO String;
run_wi_o = |commands, input, ctx| (
    let (out, _, code) = *run_wi_oec(commands, input, ctx);
    _check_exit_code(code, commands.to_iter.join(" "));;
    pure $ out
);

// Changes the current working directory to the specified path.
//
// # Parameters
// - `dir`: The directory to change to, as a string.
// - `ctx`: The context for EZS.
cd : String -> Context -> IO ();
cd = |dir, ctx| (
    when(ctx.@execution_trace, 
        eprintln("cd " + dir)
    );;
    let code = *dir.borrow_c_str_io(|c_str| FFI_CALL_IO[CInt chdir(Ptr), c_str]);
    when(code != 0.to_CInt, 
        let msg = "error: Failed to change directory to " + dir;
        eprintln(msg);;
        exit(1)
    );;
    pure()
);

// Check the exit status and exit the program if it is `signaled` or `wait_failed`.
//
// Returns the exit code if the status is `exit`.
_check_exit_status : ExitStatus -> String -> IO U8;
_check_exit_status = |status, command| (
    let command = "`" + command + "`";
    match status {
        exit(code) => (
            pure $ code
            // let msg = "error: " + command + " exited with code " + code.to_string;
        ),
        signaled(signal) => (
            let msg = "error: " + command + " terminated by signal " + signal.to_string;
            eprintln(msg);;
            exit(1)
        ),
        wait_failed() => (
            let msg = "error: " + "Failed to waiting termination of " + command;
            eprintln(msg);;
            exit(1)
        )
    }
);

// Check the exit code of a command and exit the program if it is not 0.
_check_exit_code : U8 -> String -> IO ();
_check_exit_code = |code, command| (
    when(code != 0_U8, 
        let msg = "error: " + command + " exited with code " + code.to_string;
        eprintln(msg);;
        exit(1)
    );;
    pure()
);

// TODO: Add `ez_cwd : IO String`;
